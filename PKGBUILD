# Maintainer: Marius Lindvall <(firstname) {cat} varden {dog} info>

pkgbase=fisheye-stitcher-git
pkgname=(fisheye-stitcher-git fisheye-mls-map-3840x1920-git)
pkgver='2.0.r8.g7c0a946'
pkgrel=1
pkgdesc="Stitch images generated by dual-fisheye cameras."
arch=('x86_64')
url='https://github.com/drNoob13/fisheyeStitcher'
license=('MIT')
depends=('opencv' 'vtk')
makedepends=('git' 'cmake')
provides=('fisheye-stitcher')
conflicts=('fisheye-stitcher')
source=("git+https://github.com/drNoob13/fisheyeStitcher.git")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/fisheyeStitcher"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${srcdir}/fisheyeStitcher"
	# patch for opencv 4.x
	find . -type f -name \*.cpp -exec sed -rie 's/CV_(INTER_LINEAR|TM_CCORR_NORMED|CAP_PROP)/cv::\1/g' {} \; -o -path ./build -prune
}

build() {
	export CFLAGS+=" ${CPPFLAGS}"
	export CXXFLAGS+=" ${CPPFLAGS}"
	cmake -B build -S "${srcdir}/fisheyeStitcher" -DCMAKE_BUILD_TYPE='None' -DCMAKE_INSTALL_PREFIX='/usr' -Wno-dev
	make -C build
}

package_fisheye-stitcher-git() {
	cd "${srcdir}"
	install -Dm755 build/bin/fisheyeStitcher "$pkgdir/usr/bin/fisheyeStitcher"
	install -Dm644 build/libstitcher.so "$pkgdir/usr/lib/libstitcher.so"
	install -Dm644 fisheyeStitcher/LICENSE "$pkgdir/usr/share/licenses/fisheye-stitcher/LICENSE"
}

package_fisheye-mls-map-3840x1920-git() {
	pkgdesc="fisheye-stitcher remap grid for Samsung Gear 360 (SM-C200)"
	arch=(any)
	depends=()
	provides=(fisheye-mls-map-3840x1920)
	conflicts=(fisheye-mls-map-3840x1920)
	install -Dm644 fisheyeStitcher/utils/grid_xd_yd_3840x1920.yml.gz "$pkgdir/usr/share/fisheye-stitcher/grid_xd_yd_3840x1920.yml.gz"
	install -Dm644 fisheyeStitcher/LICENSE "$pkgdir/usr/share/licenses/fisheye-mls-map-3840x1920/LICENSE"
}
