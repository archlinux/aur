# Maintainer: Bruno Ancona <bruno at powerball253 dot com>

_pkgname="hyprland"
pkgname="${_pkgname}-nvidia-screenshare-git"
pkgver=r734.354e265
pkgrel=1
pkgdesc="Hyprland dynamic Wayland compositor with with workarounds applied for screen sharing on NVIDIA"
arch=(any)
url="https://github.com/vaxerski/Hyprland"
license=('BSD')
depends=(
	libxcb
	xcb-proto
	xcb-util
	xcb-util-keysyms
	libxfixes
	libx11
	libxcomposite
	xorg-xinput
	libxrender
	pixman
	wayland-protocols
	cairo
	pango
	polkit
	glslang
	libinput
	libxcb
	libxkbcommon
	opengl-driver
	pixman
	wayland
	xcb-util-errors
	xcb-util-renderutil
	xcb-util-wm
	seatd
	vulkan-icd-loader
	vulkan-validation-layers
	xorg-xwayland)
makedepends=(
	git
	cmake
	ninja
	gcc
	gdb
	meson
	vulkan-headers
	wayland-protocols
	xorgproto)
source=("${_pkgname}::git+https://github.com/vaxerski/Hyprland.git"
        "0001-nvidia-format-workaround.patch")
conflicts=("${_pkgname}")
provides=(hyprland)
sha256sums=('SKIP' '0ed607f47d04001220b54245da94618c7fdbdeddcd003d55b34858585f8f5ac6')
options=(!makeflags !buildflags)

pkgver() {
  cd "$_pkgname"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
	cd "${srcdir}/${_pkgname}"
	git submodule update --init

    cd wlroots
    patch -p1 < "${srcdir}/0001-nvidia-format-workaround.patch"
    cd ..

	make all
}

package() {
	cd "${srcdir}/${_pkgname}"
	mkdir -p "${pkgdir}/usr/share/wayland-sessions"
	mkdir -p "${pkgdir}/usr/share/hyprland"
	install -Dm755 build/Hyprland -t "${pkgdir}/usr/bin"
	install -Dm755 hyprctl/hyprctl -t "${pkgdir}/usr/bin"
	install -Dm644 assets/*.png -t "${pkgdir}/usr/share/hyprland"
	install -Dm644 example/hyprland.desktop -t "${pkgdir}/usr/share/wayland-sessions"
	install -Dm644 example/hyprland.conf -t "${pkgdir}/usr/share/hyprland"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
}
