# Generated by debtap
# Maintainer: Caio Prado <cagprado at qq.com>
#
# Initial setup thanks to debtap (https://github.com/helixarch/debtap)
pkgname=vyprvpn-linux-cli
pkgver=1.7
pkgrel=1
pkgdesc="A simple service and command line interface to manage NetworkManager based connections to VyprVPN."
arch=('i686' 'x86_64')
url="http://www.goldenfrog.com"
license=('custom')
depends=('networkmanager' 'networkmanager-openvpn' 'networkmanager-pptp' 'networkmanager-vpnc')
options=('!strip' '!emptydirs')

backup=('etc/vyprvpn/vyprvpn_service.conf')
install=$pkgname.install

source=("$pkgname.install")
source_i686=("https://support.goldenfrog.com/hc/article_attachments/360010579171/vyprvpn-linux-cli-1.7.i386.deb")
source_x86_64=("https://support.goldenfrog.biz/hc/article_attachments/360010562332/vyprvpn-linux-cli-1.7.amd64.deb")

sha256sums=('310cfdcf3d8b84789923139f3ce918003ad743bddf8bed72bc23bef90a43240b')
sha256sums_i686=('c475aa3e58a998fa8c837be4be4dd56f86aa7e57578afabaf7079739bc41b5c4')
sha256sums_x86_64=('3f589a66eddf466ff6600882b10aec55946034106e2b70388042f22919af7d65')

prepare(){
	# Extract data
	tar xf data.tar.xz
}

package(){
	install -d "$pkgdir"/usr
	cp -R "$srcdir"/usr/bin "$pkgdir"/usr
	cp -R "$srcdir"/usr/share "$pkgdir"/usr
	cp "$srcdir"/usr/sbin/* "$pkgdir"/usr/bin
	chmod 755 "$pkgdir"/usr/bin/*

	install -d "$pkgdir"/etc
	install -d "$pkgdir"/usr/lib
	cp -R "$srcdir"/etc/vyprvpn "$pkgdir"/etc
	cp -R "$srcdir"/etc/systemd "$pkgdir"/usr/lib
	chmod 644 "$pkgdir"/usr/lib/systemd/system/vyprvpn.service

	touch "$pkgdir"/etc/vyprvpn/vyprvpn_service.conf
	chmod 600 "$pkgdir"/etc/vyprvpn/vyprvpn_service.conf

	sed -i '/ExecStop/c\KillMode=process' "$pkgdir"/usr/lib/systemd/system/vyprvpn.service
}
