# Maintainer: √Årni Dagur <arnidg@protonmail.ch>

pkgname='xcp'
pkgver='0.9.0'
pkgrel=1
pkgdesc="An exctended 'cp', written in Rust"
arch=('x86_64')
url='https://github.com/tarka/xcp'
license=('GPL-3.0')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tarka/xcp/archive/v$pkgver.tar.gz")
sha512sums=('861050137b425ca6b4d79a35c03d7b66ca79c8b6cbc98c66f6d35a6399e6e555e67d215ae837bfb60f5096bf82eb48a4f0e21181c5b7f2cd510c08cc523f0bc8')

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/xcp
}
