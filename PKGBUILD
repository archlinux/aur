# Maintainer: tarball <bootctl@gmail.com>
# Contributor: √Årni Dagur <arnidg@protonmail.ch>

pkgname='xcp'
pkgver='0.10.2'
pkgrel=1
pkgdesc="An exctended 'cp', written in Rust"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url='https://github.com/tarka/xcp'
license=('GPL3')
depends=('glibc' 'gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('cd33c28ab573031f42ecf54735b41f3ef4e75d39b953afee6cf22f22d982b879074df00faea5e1b45f0a3df8514fac8b237efec30a5afd890106259846cba059')

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/xcp
}
