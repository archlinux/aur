# Maintainer:
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

_module="diffusers"
_pkgname="python-$_module"
pkgname="$_pkgname${_pkgtype:-}"
pkgver=0.24.0
pkgrel=2
pkgdesc='Pretrained diffusion models for image and audio generation in PyTorch'
url='https://github.com/huggingface/diffusers'
license=('Apache-2.0')
arch=('any')

makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)

_pkgsrc="$_module-$pkgver"
source=("$_module-$pkgver"::"$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('148d926605782d262e512d6ba819ff3b45dad7f2e84c7f9d2a5fc0b6e793e6a7')

build() {
  cd "$_pkgsrc"
  python -m build --no-isolation --wheel
}

package() {
  depends+=(
    'python-beautifulsoup4'
    'python-importlib-metadata'
    'python-msgpack'
    'python-numpy'
    'python-pillow'
    'python-pytorch'
    'python-requests'
    'python-scipy'
    'python-tqdm'

    # AUR
    'python-huggingface-hub'
  )

  cd "$_pkgsrc"
  python -m installer --destdir="${pkgdir:?}" dist/*.whl
}
