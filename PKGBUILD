# Maintainer: Malte JÃ¼rgens <maltejur@dismail.de>

pkgname=discord-screenaudio
pkgver=1.9.0
pkgrel=1
pkgdesc="Custom discord client with the ability to stream desktop audio"
arch=("x86_64")
url="https://github.com/maltejur/discord-screenaudio"
license=("GPL-3.0")
depends=(qt5-base qt5-webengine knotifications kxmlgui kglobalaccel pipewire xdg-desktop-portal-impl)
makedepends=(qt5-base qt5-webengine knotifications kxmlgui kglobalaccel pipewire cmake)
optdepends=("nodejs: Rich presence support through arRPC")
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/maltejur/$pkgname/archive/refs/tags/v$pkgver.tar.gz"
  "rohrkabel-1.2.tar.gz::https://github.com/Soundux/rohrkabel/archive/refs/tags/v1.2.tar.gz"
)
sha256sums=(
  "8e29a4d9a7f199659fff13d484fca31d42bc699acb0e6591734f2daf66c09374"
  "e167e50d4c633a2b96dde5faa28cd83dd56828868c669fcf86c7e0ade7db1dcd"
)

prepare() {
  rm -rf $pkgname-$pkgver/submodules/rohrkabel
  mv -v rohrkabel-1.2 $pkgname-$pkgver/submodules/rohrkabel
  echo "$pkgver" >$pkgname-$pkgver/version.txt
}

build() {
  cmake -B build -S $pkgname-$pkgver -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build --config Release
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
