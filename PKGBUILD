# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=Metab
_pkgver=1.33.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="An R Package for a High-Throughput Analysis of Metabolomics Data Generated by GC-MS"
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=('GPL-2.0-or-later')
depends=(
  r-pander
  r-svdialogs
  r-xcms
)
optdepends=(
  r-biocgenerics
  r-runit
)
source=("https://bioconductor.org/packages/3.17/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('8b19a02165b42e95410fbcf5c44ee352')
b2sums=('47a5849bb065e033cf96e4c2c05e9fafef02ceb715da508c889b6124efce179ea4b53f05b7a0490f5b7310bf3509eff3452f31429dae285d2e29644a3c1d7286')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
