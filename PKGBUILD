# Maintainer: Brycen Granville <brycengranville@outlook.com>
pkgbase=snapx
pkgname=(snapx snapx-ui)
pkgver=0.4.0
pkgrel=1
pkgdesc="Screenshot tool that handles images, text, and video (fork of ShareX)"
arch=('x86_64' 'aarch64')
url="https://github.com/BrycensRanch/SnapX"
license=('GPL-3.0-or-later')
makedepends=(
    'git'
    'dotnet-sdk>=9.0'
    'clang'
    'zlib'
)

source=("$pkgbase::git+https://github.com/BrycensRanch/SnapX.git")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgbase"
    ver=$(./build.sh --version | head -n1 | tr -d '\r\n')
    # Keep digits, dots and letters, plus dashes or underscores
    # Then replace forbidden hyphens with underscores
    ver="${ver//-/_}"
    # Remove colons, slashes, whitespace completely
    ver="${ver//[:\/[:space:]]/}"
    echo "$ver"
}

build() {
    cd "$pkgbase"
    export VERSION=$pkgver
    export PKGTYPE=ARCH
    ./build.sh --configuration Release
}

check() {
    cd "$pkgbase"
    Output/snapx/snapx --version
}

package_snapx() {
    depends=(
        'ffmpeg'
        'fontconfig'
        'freetype2'
        'openssl'
        'icu'
        'at'
        'sudo'
        'libxrandr'
        'libxcb'
        'dbus'
        'xdg-utils'
    )

    cd "$pkgbase"
    ./build.sh install --prefix /usr --dest-dir "${pkgdir}" --skip compile
    rm -f "${pkgdir}/usr/bin/snapx-ui"
    rm -f "${pkgdir}/usr/lib/snapx/snapx-ui"
    rm -f "${pkgdir}/usr/lib/snapx/libSkiaSharp.so"
    rm -f "${pkgdir}/usr/lib/snapx/libHarfBuzzSharp.so"
}

package_snapx-ui() {
    pkgdesc="Screenshot tool that handles images, text, and video (fork of ShareX). SnapX Avalonia-based UI (works best on X11)"
    depends=('snapx')

    cd "$pkgbase"
    ./build.sh install --prefix /usr --dest-dir "${pkgdir}" --skip compile

    # Keep the files only for Avalonia UI
    rm -rf "${pkgdir}/usr/share"
    rm -f "${pkgdir}/usr/bin/snapx"
    rm -f "${pkgdir}/usr/lib/snapx/snapx"
    rm -f "${pkgdir}/usr/lib/snapx/libe_sqlite3.so"
}
