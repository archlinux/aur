# Maintainer: dreieck

# PKGBUILD last time manually edited: At least on 2024-01-14.

_pkgname=idos-package-updater-script
pkgname="${_pkgname}"
epoch=0
pkgver=20210704.1
pkgrel=3
pkgdesc="Bash script which automates the process of updating installed IDOS timetable browser related packages by reinstalling and thus fetching the newest version."
arch=('any')
license=('custom')
groups=(
  "idos-timetable"
)
depends=(
  "util-linux" # for 'column'-executable.
  "pacman"
  "yay"
)
makedepends=()
optdepends=()
provides=()
conflicts=()
replaces=()

source=(
  "idos-packages-update.sh"
  "copying.txt"
)

sha256sums=(
  "d1745165ce2dcf510f73e9201a1f7d70c3d2d9430b9c84d119cf1e3baf22105d"
  "c3cbff25307e905545788f5c74cc137d79706c60549092f2a37919b93cf55ee3"
)


pkgver() {
  cd "${srcdir}"
  ./idos-packages-update.sh --version
}


prepare() {
  chmod a+x "${srcdir}/idos-packages-update.sh"
}


package() {
  _instdirbase='/opt/idos-timetable'
  _instdir="${pkgdir}/${_instdirbase}"
  _licdirbase="/usr/share/licenses/${pkgname}"
  _licdir="${pkgdir}/${_licdirbase}"
  _execdirbase='/usr/bin'
  _execdir="${pkgdir}/${_execdirbase}"
  
  install -v -D -m755 "${srcdir}/idos-packages-update.sh" "${_instdir}/idos-packages-update.sh"
  install -v -D -m644 "${srcdir}/copying.txt" "${_licdir}/copying.txt"
  install -v -d -m755 "${_execdir}"

  ln -sv "${_instdirbase}/idos-packages-update.sh" "${_execdir}/"
}
