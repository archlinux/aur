# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=junit-system-rules
pkgver=1.19.0
_commit=f4782ebfd72c08bd3f00aa04ad20fe499c5463b4 # signed commit corresponding to tag system-rules-1.19.0
pkgrel=4
pkgdesc='A collection of JUnit rules for testing code that uses java.lang.System'
arch=('any')
url='https://stefanbirkner.github.io/system-rules/'
license=('CPL')
depends=('junit')
makedepends=('git' 'jdk8-openjdk' 'maven' 'strip-nondeterminism')
source=("$pkgname::git+https://github.com/stefanbirkner/system-rules.git?signed#commit=$_commit"
        'junit-system-rules_update-lib-parent.patch::https://github.com/stefanbirkner/system-rules/commit/6a4fe4c8ccaa3f9234b8eec75deeaa42b5154568.patch')
sha512sums=('SKIP'
            '42d4172d32a1376a0cb6a33f5ba150929bd8d11ef3b73b67a3e2a6f12b0b302f991066b2f85bda55d2c2e0e30cb8e6cde0c6067a76cad142da366c78138f0666')
validpgpkeys=('F4AF40991AECE57728B0034F9ECE1F68817F4996') # Stefan Birkner <mail@stefan-birkner.de>

prepare() {
	cd "$pkgname"
	# Update lib-parent to latest version 16 to fix build failure (GitHub PR #76)
	patch --strip=1 --input="$srcdir/junit-system-rules_update-lib-parent.patch"
}

build() {
	cd "$pkgname"

	# The package cannot be compiled with newer Java versions (but works fine at runtime)
	# (https://github.com/stefanbirkner/system-rules/issues/65#issuecomment-402837535)
	export PATH="/usr/lib/jvm/java-8-openjdk/bin:$PATH"

	mvn -DskipTests=true clean package

	# Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
	# (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" "target/system-rules-$pkgver.jar"
}

check() {
	cd "$pkgname"
	mvn test
}

package() {
	cd "$pkgname"
	install -Dm644 "target/system-rules-$pkgver.jar" -t "$pkgdir/usr/share/java/junit-system-rules"
	ln -s "system-rules-$pkgver.jar" "$pkgdir/usr/share/java/junit-system-rules/system-rules.jar"
}
