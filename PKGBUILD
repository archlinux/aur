# Maintainer: Damian Miller <rysas@mail.naimad.dev>
pkgname=lumoria
pkgver=0.1.4
pkgrel=1
pkgdesc="A Linux installer and launcher for Final Fantasy XI"
arch=('x86_64')
url="https://github.com/Windower/Lumoria"
license=('GPL-3.0-or-later')
depends=(
  'gtk4'
  'json-glib'
  'libadwaita'
  'libarchive'
  'libgee'
  'libmspack'
  'libsoup3'
  'libmanette'
  'vte4'
)
makedepends=(
  'meson'
  'vala'
)
checkdepends=('appstream-glib')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('d6efc602961029df202a3286ce49e6ea75fe1cf4319e7118fe019866a5a6cb9e')

build() {
  arch-meson "Lumoria-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build --no-rebuild --print-errorlogs
}

package() {
  meson install -C build --no-rebuild --destdir "$pkgdir"
}
