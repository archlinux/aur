# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgdesc='Omron LUNA-I/LUNA-88K, Sharp X68030, and NEWS NWS-1750 emulator'
pkgname=nono
pkgver=0.6.4
pkgrel=1
url=http://www.pastel-flower.jp/~isaki/nono
license=(custom)
arch=(x86_64)
depends=(libbsd libkqueue termcap wxgtk3)
makedepends=(bmake freebsd-mk gettext)
source=("${url}/archive/${pkgname}-${pkgver}.tar.gz")
b2sums=('5f795511152dcd1b096df10fd22e3d47f91baf00a1d9d84f8e158a1a43ecd3520a1ff4d95437d9ddc18871a7fcf9039036eaa7af6fc33be4506ad59da5d5d057')

prepare ()
{
	# The configure script searches for <kqueue/sys/event.h> instead of the
	# regular location <sys/event.h> as used by the libkqueue package, so
	# provide a local directory with the expected layout.
	mkdir -p "${srcdir}/include/kqueue/"
	ln -vsnf /usr/include/sys "${srcdir}/include/kqueue/sys"
}

build ()
{
	cd "${pkgname}-${pkgver}"

	./configure \
		CPPFLAGS="${CPPFLAGS} -I${srcdir}/include" \
		WX_CONFIG=/usr/bin/wx-config-gtk3 \
		--prefix=/usr

	# The configuration generated by autoconf does NOT include CPPFLAGS,
	# so add the include directory for the mangled libkqueue headers now.
	sed -i "/^C\(C\|XX\)=/s,$, -I${srcdir}/include," \
		Makefile.cfg

	bmake -DNOTEST -m/usr/share/mk-freebsd
}

package ()
{
	cd "${pkgname}-${pkgver}"
	bmake -DNOTEST -m/usr/share/mk-freebsd DESTDIR="${pkgdir}" install
	install -Dm644 doc/nono-license.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
