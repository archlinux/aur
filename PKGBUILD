# Maintainer: TÃ©rcio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>

pkgname=r3d-sdk
pkgver=8.4.0
pkgrel=1
arch=('x86_64')
pkgdesc="SDK to load and decode R3D files generated by RED cameras"
url="https://www.red.com/developers"
license=('custom')
depends=('gcc-libs' 'util-linux-libs')
makedepends=('poppler')
options=('!strip')
source=("https://downloads.red.com/software/sdk/R3D/${pkgver%.*}/R3DSDKv${pkgver//./_}.zip")
sha512sums=('39f387124409e3c3ffa32db65ccf5db14d5ef9692fc5a0f595f592bfe6a799886d37275a5cfa596dba52dcbf8e7170be6781a79e61512f799499ade8834258d7')

prepare(){
  pdftotext -layout "R3DSDKv${pkgver//./_}/SDK License Agreement.pdf" 2>&-
  mv "R3DSDKv${pkgver//./_}/SDK License Agreement.txt" "$srcdir"
}

package() {
  install -d "$pkgdir/opt"
  install -d "$pkgdir/usr/share/licenses/$pkgname"

  cp -dr --no-preserve=ownership "R3DSDKv${pkgver//./_}" \
                                 "$pkgdir/opt/R3DSDK"

  cp --no-preserve=ownership "$srcdir/SDK License Agreement.txt" \
                             "$pkgdir/usr/share/licenses/$pkgname"
}
