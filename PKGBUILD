# Maintainer: zeroclaw-labs <bot@zeroclaw.dev>
pkgname=zeroclawlabs
_reponame=zeroclaw
pkgver=0.8.0
pkgrel=1
pkgdesc="Zero overhead. Zero compromise. 100% Rust. The fastest, smallest AI assistant."
arch=('x86_64' 'aarch64')
url="https://github.com/zeroclaw-labs/zeroclaw"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'openssl')
makedepends=('cargo' 'git' 'nodejs' 'npm')
provides=('zeroclaw' 'zerocode')
conflicts=('zeroclaw' 'zerocode')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/zeroclaw-labs/zeroclaw/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('201e25ac0cbc757d2176291182f3a9e228109c4f92b28ca90122b4beeb1ca826')

prepare() {
  cd "${_reponame}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${_reponame}-${pkgver}"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  # Build web dashboard (served from filesystem at runtime)
  cd web && npm ci && cd ..
  cargo web build

  cargo build --frozen --profile dist --features channel-matrix,channel-lark
  cargo build --frozen --profile dist -p zerocode
}

package() {
  cd "${_reponame}-${pkgver}"
  install -Dm0755 -t "${pkgdir}/usr/bin/" "target/dist/zeroclaw"
  install -Dm0755 -t "${pkgdir}/usr/bin/" "target/dist/zerocode"

  # Install web dashboard assets (served from filesystem at runtime)
  install -dm0755 "${pkgdir}/usr/share/${pkgname}/web/dist"
  cp -r web/dist/* "${pkgdir}/usr/share/${pkgname}/web/dist/"

  install -Dm0644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
  install -Dm0644 LICENSE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
