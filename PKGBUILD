# Maintainer: Guoyi Zhang <guoyizhang at malacology dot net>

_pkgname=RJSONIO
_pkgver=1.3-1.9
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Serialize R Objects to JSON, JavaScript Object Notation"
arch=(x86_64)
url="https://cran.r-project.org/package=$_pkgname"
license=(BSD)
depends=(
  r
)
source=("https://cran.r-project.org/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('d42747c9c3be446a3aefa2b9fa28023c')
b2sums=('24741269513cc5259c03420879c1b926ebd6927c9a361db154853b583ac116519df4c68a9860cb165c0c58021777074b5778097bfc2b4342474a6387fe57af94')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "/usr/lib/R/library/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
