# Maintainer: MuntasirSZN <muntasir.joypurhat@gmail.com>
# Maintainer: cordx56 <cordx56@cordx.cx>

pkgname=rustowl-bin
pkgver=0.4.0
pkgrel=2
pkgdesc='Visualize Ownership and Lifetimes in Rust'
url='https://github.com/cordx56/rustowl'
license=('MPL-2.0')
makedepends=()
depends=()
conflicts=('rustowl-git' 'rustowl')
arch=('x86_64' 'aarch64')
source_aarch64=("https://github.com/cordx56/rustowl/releases/download/v${pkgver}/rustowl-aarch64-unknown-linux-gnu.tar.gz")
source_x86_64=("https://github.com/cordx56/rustowl/releases/download/v${pkgver}/rustowl-x86_64-unknown-linux-gnu.tar.gz")
sha256sums_x86_64=('054c8947babf38b9868e20431b109204ff22864ae49673d0e0967847d6d7679a')
sha256sums_aarch64=('e374c80724065b007c54868bb5b870588244e34f36e7807d68c578b5c1dead2a')

package() {
    install -d -m 755 "$pkgdir/opt/rustowl"
    cp -a sysroot/ "$pkgdir/opt/rustowl/"
    install -Dm0755 -t "$pkgdir/usr/bin/" "rustowl"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/rustowl/LICENSE"
    install -Dm644 man/rustowl.1 "$pkgdir/usr/share/man/man1/rustowl.1"
    install -Dm644 "completions/rustowl.bash" "${pkgdir}/usr/share/bash-completion/completions/rustowl"
    install -Dm644 "completions/_rustowl" "${pkgdir}/usr/share/zsh/site-functions/_rustowl"
    install -Dm644 "completions/rustowl.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/rustowl.fish"
    install -Dm644 "completions/rustowl.elv" "${pkgdir}/usr/share/elvish/completions/rustowl.elv"
    install -Dm644 "completions/_rustowl.ps1" "${pkgdir}/usr/share/powershell/Modules/Rustowl/_rustowl.ps1"
}
