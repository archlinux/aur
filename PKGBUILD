# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=o
pkgver=2.12.1
pkgrel=2
pkgdesc='Small, fast and VT100-compatible text editor'
arch=(x86_64)
url='https://github.com/xyproto/o'
license=(BSD)
makedepends=(git go-pie upx)
source=("git+$url#tag=$pkgver")
sha256sums=('SKIP')
options=(!strip)

build() {
  cd "$pkgname"

  # Using gccgo gives a smaller size, but forces AUR users to juggle between go and gccgo, which is unfortunate
  #go build -buildmode=pie -gccgoflags="-s -w $LDFLAGS"

  go build -buildmode=pie -gcflags="all=-trimpath=$PWD" -asmflags "all=-trimpath=$PWD" -ldflags "-s -w -extldflags $LDFLAGS"
  upx o
}

package() {
  install -Dm755 $pkgname/o "$pkgdir/usr/bin/o"
  install -Dm644 $pkgname/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
