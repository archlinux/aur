# Maintainer: AltoXorg <atrl101 AT yahoo DOT com>

_reponame=Shipwright
_libultraship_commit=2ddffdb5b60377a09a4a198a8fd67726951bbb27
pkgbase=soh
pkgname=(soh soh-otr-exporter)
pkgver=7.0.0
pkgrel=1
arch=("x86_64" "i686")
url="https://shipofharkinian.com/"
_depends_soh=("sdl2" "sdl2_net" "libpulse" "glew" "zenity")
_depends_soh_otr_exporter=("libpng")
depends=("${_depends_soh[@]}" "${_depends_soh_otr_exporter[@]}")
makedepends=("cmake" "ninja" "python" "curl" "lsb-release" "libxrandr" "libxinerama" "libxi" "glu" "boost")
source=("${_reponame}-${pkgver}.tar.gz::https://github.com/HarbourMasters/${_reponame}/archive/refs/tags/${pkgver}.tar.gz"
        "libultraship-${_libultraship_commit}.tar.gz::https://github.com/Kenix3/libultraship/archive/${_libultraship_commit}.tar.gz"
        "soh.desktop"
        "soh-misc-otr-patches.patch"
        "lus-install-paths.patch"
        "otrgui-wrapper.sh")
sha256sums=('626d4e8c96b7889228fe064b930688b4d56722450e03b0c2057076f1f979a2e5'
            '8ae6999a16053c561f8722e8c363b046b83277108499b20cd77892773a5009f3'
            '25aebd34f6ad49073d8a5ce6915b6fa290470fc6d62a8143abe07a25707ff4a2'
            '200cba1e21ef57cf80bd8962ca6d5631062a7c056c897c2a4d58bfb8217ddef7'
            '808049e8f02d78188490afc4632e3bec0253d7c3c62d85492172938e35d1165f'
            '6e735877e7bba81f9f308f6eabbdfe5354f2c331a9acf9a16ab02a5681f2c25f')

# Changable options for debugging:
__generate_headers=0  # Generate OTR (unnecessary) and asset headers. **requires rom**

SHIP_PREFIX=/opt/soh

prepare() {
  cd "${srcdir}/${_reponame}-${pkgver}"

  # Patch libultraship
  pushd ../libultraship-${_libultraship_commit} > /dev/null
  patch -Np2 -i "${srcdir}/lus-install-paths.patch"
  popd > /dev/null
  # Symlink libultraship
  rm -r libultraship
  ln -sf ../libultraship-${_libultraship_commit} libultraship

  # Commit 66b2d2d5fae75ad4d4b2a5106bf70bf8321aa88c permamently adds pregenerated asset headers
  # so you don't need to activate this one unless told to do so
  if [ "$__generate_headers" != 1 ]; then
    # Required asset headers generated by ZAPD/TR so we don't require a rom to create these files.
    # Only needed headers are present to compensate file size requirements.
    # NOTE: Refer to soh-git's PKGBUILD for creating the header archive.

    #tar -Jxf ../assets-headers-gc_nmq_pal_f.tar.xz
    true
  else
    # check for any roms in the directory where PKGBUILD resides
    # and copy them to Shipwright/OTRExporter. It doesn't matter
    # which rom we'll be using, let extract_assets.py do the guessing.
    roms=( "${startdir}/"*.*64 )
    if (( "${#roms[@]}" )); then
      cp "${roms[@]}" OTRExporter
    else
      echo "NO ROMS FOUND! Please place them in \"${startdir}\"."
      return 1
    fi
  fi

  patch -Np1 -i "${srcdir}/soh-misc-otr-patches.patch"
}

build() {
  cd "${srcdir}/${_reponame}-${pkgver}"

  CFLAGS="${CFLAGS/-Werror=format-security/}" \
  CXXFLAGS="${CXXFLAGS/-Werror=format-security/}" \
    cmake -Bbuild -GNinja -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=$SHIP_PREFIX .

  cmake --build build --target ZAPD --config Release

  if [ "$__generate_headers" = 1 ]; then
    cmake --build build --target ExtractAssetsHeaders
  else
    cd OTRExporter

    rm -f soh.otr || true
    rm -rf Extract || true
    mkdir Extract
    cp -r assets Extract/assets

    ../build/ZAPD/ZAPD.out botr -se OTR --norom

    cd ..
  fi

  cmake --build build --target soh --config Release
  cmake --build build --target OTRGui --config Release
}

package_soh() {
  pkgdesc="An unofficial port of The Legend of Zelda Ocarina of Time for PC, Wii U, and Switch"
  depends=("${_depends_soh[@]}")
  license=("unknown")
  install=soh.install
  optdepends=("soh-otr: OTR asset file in order to run"
              "soh-otr-mq: OTR asset file in order to run (Master Quest)")

  cd "${srcdir}/${_reponame}-${pkgver}"

  DESTDIR="${pkgdir}" cmake --install build/soh

  install -dm755 "${pkgdir}/usr/bin/"

  ln -s /opt/soh/soh.elf "${pkgdir}/usr/bin/soh"
  install -Dm644 "${srcdir}/soh.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 soh/macosx/sohIcon.png "${pkgdir}/usr/share/pixmaps/soh.png"

  install -Dm644 OTRExporter/soh.otr "${pkgdir}/${SHIP_PREFIX}/soh.otr"

  install -dm755 "${pkgdir}/usr/share/doc/soh" "${pkgdir}/usr/share/doc/soh/docs"
  install -Dm644 "README.md" "${pkgdir}/usr/share/doc/soh"
  find docs -exec install -Dm644 {} "${pkgdir}/usr/share/doc/soh/docs" \;
}

package_soh-otr-exporter() {
  pkgdesc="OTR generation tools for SoH, including asset XML files and a simple GUI tool"
  license=("MIT")
  depends=("${_depends_soh_otr_exporter[@]}")
  optdepends=("zenity: OTRGui file chooser"
              "kdialog: OTRGui file chooser (KDE)")

  cd "${srcdir}/${_reponame}-${pkgver}"

  DESTDIR="${pkgdir}" cmake --install build/OTRGui

  install -dm755 "${pkgdir}/usr/bin/"
  install -Dm755 "${srcdir}/otrgui-wrapper.sh" "${pkgdir}/usr/bin/OTRGui"
  ln -s ${SHIP_PREFIX}/assets/extractor/ZAPD.out "${pkgdir}/usr/bin/ZAPD"

  # Change the external xml folder path so that it always points to this package
  find "${pkgdir}/${SHIP_PREFIX}/assets/extractor" -maxdepth 1 -name Config_\*.xml -exec \
    sed -i "/ExternalXMLFolder/s,assets/extractor,${SHIP_PREFIX}/&," {} +

  install -dm755 "${pkgdir}/usr/share/licenses/soh"
  install -Dm644 "OTRExporter/LICENSE" "${pkgdir}/usr/share/licenses/soh"
}
