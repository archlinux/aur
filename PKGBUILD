# Maintainer: Einhard Leichtfu√ü <alguien@respiranto.de>
# Contributor: Tai Chi Minh Ralph Eastwood <tcmreastwood@gmail.com>
pkgname=dict-gcide
_major_debver=0.48
_debver="${_major_debver}.5+nmu1"
pkgver=0.53
pkgrel=1
pkgdesc="GNU version of the Collaborative International Dictionary of English for dictd et al."
arch=('any')
url="https://gcide.gnu.org.ua/"
license=('GPL3')
optdepends=('dictd: dict client and server')
makedepends=('dictd' 'sed' 'libmaa')
provides=('dictd-gcide')
conflicts=('dictd-gcide')
install="${pkgname}.install"
source=('fixes.sed'
        'post_webfilter.sed'
        'check.sed'
        "https://deb.debian.org/debian/pool/main/d/${pkgname}/${pkgname}_${_debver}.tar.xz"
        "https://ftp.gnu.org/gnu/gcide/gcide-${pkgver}.tar.xz"{,.sig})
sha512sums=('912e3a6ba1db08a75df4cd7aeaa8b6d4acf02a6e7982a992f0023d979251b0dc46877ef76e2d16a9fc5921e35debaeb339cde19b033d94f9797325320c3fb43b'
            'e7c6766b51ef92c4d8669394b6a71f8c4d7249ac63a1eb940f0a3017cb5ff6841f0f4bce7a60ffbcf180801d92b068db6ca46afd1dceac06a9c37c59dcddbfb3'
            '1304bdce3776355fcd7827cebbf670ae34377bda78b04bf271ba10a25d92e33f6ec69644e168c30a6ec47808ebacc461de47d9494a5a2a5894d58c21be045973'
            '775775fbcf49f6f9e29ad60919692f02b920efe710bc311674cc37f045b69ef9ee68e2fe4045a39a90485a0661581267ce13590f893eb3a896c947d5b760638e'
            '0eb641cdc8186757bb7e221753c9a42f28ead25b72b18cacebf2326ee239c0e5ab2d41b61928371168ff2c8a298aeaa93d50656d8165ae155699c1d359e4918f'
            'SKIP')
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732')


prepare()
{
	cd "${pkgname}-${_debver}"

	sed -Ei \
		"s/\"(The Collaborative International Dictionary of English) v.${_major_debver}\"/\"\\1 v.${pkgver}\"/" \
		scan.l

	# Remove autogenerated autotools files.
	rm config.guess config.h.in config.sub configure install-sh
}


build()
{
	cd ${pkgname}-${_debver}

	autoreconf -fis
	./configure
	make -j1

	# Do the conversion explicitly, instead of `make db', to account for all
	# the differences to the original build process.
	# LANG=C is required so that the index file is properly sorted.
	../fixes.sed "../gcide-${pkgver}"/CIDE.? \
		| sed -f debian/sedfile \
		| ./webfilter \
		| ../post_webfilter.sed \
		| tee pre_webfmt.data \
		| LANG=C ./webfmt -c

	# `dictzip -v' neclects to print a final newline.
	dictzip -v gcide.dict
	printf '\n'
}


check()
{
	errors="$(./check.sed < "${pkgname}-${_debver}/pre_webfmt.data")"

	if test -n "$errors"
	then
		printf 'Errors found:\n'
		printf '%s\n' "$errors"
		return 1
	fi
}


package()
{
	install -m 0755 -d "${pkgdir}/usr/share/dictd"
	install -m 0644 -t "${pkgdir}/usr/share/dictd/" \
		"${pkgname}-${_debver}"/gcide.{dict.dz,index}

	install -m 0755 -d "${pkgdir}/usr/share/doc/dict-gcide"
	install -m 0644 -t "${pkgdir}/usr/share/doc/dict-gcide/" \
		"gcide-${pkgver}"/{README,INFO,pronunc.txt}
}
