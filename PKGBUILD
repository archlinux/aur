# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mediainfo-git
pkgver=23.03.r17.g1a512f2dd
pkgrel=1
pkgdesc="Utility for displaying technical and tag data for media files"
arch=('i686' 'x86_64')
url="https://mediaarea.net/en/MediaInfo"
license=('BSD')
depends=('glibc' 'libmediainfo')
makedepends=('git')
provides=("mediainfo=$pkgver")
conflicts=('mediainfo')
source=("git+https://github.com/MediaArea/MediaInfo.git")
sha256sums=('SKIP')


pkgver() {
  cd "MediaInfo"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "MediaInfo/Project/GNU/CLI"

  ./autogen.sh
  ./configure \
    --prefix="/usr"
  make
}

package() {
  cd "MediaInfo"

  make -C "Project/GNU/CLI" DESTDIR="$pkgdir" install
  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/mediainfo"
}
