# Maintainer: Simon Brulhart <simon@brulhart.me>

pkgname=python-taxi
pkgver=6.1.1
pkgrel=1
pkgdesc="Timesheeting tool that focuses on simplicity"
arch=('any')
license=('custom')
url="https://github.com/sephii/taxi"
depends=('python-appdirs' 'python-click' 'python-six')
makedepends=('python' 'python-setuptools')
optdepends=("python-taxi-zebra: Zebra backend for Taxi")
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/sephii/taxi/archive/$pkgver.tar.gz"
  taxi-complete.bash
  taxi-complete.zsh
)
sha512sums=('e6efdfaa4237f06cb582c64b771e96da14e58bd1a87f86c56b80bca4bc8c7623c482992dbb0588524fea6ee25c60abbb3e8422938b21f8473acd2ef0ce65540e'
            '646ac547f52fb9a1c8498a9d1d431a3c19b41faacd86b7344ffb8c2334e6b01b765945882e5180c7f658b8c3f6a7887a690171b624326b5b3fa3ecda59344ccc'
            'd02c0e3489dba8841eff335571088b739911d4c541623971cfef9b1628ea6960ad748e1e6c4cc1dfdd20dc88fc289f8078dbb51fa0402eed4868c45e87f4106a')

build() {
  cd "$srcdir/taxi-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/taxi-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$srcdir"
  # Completion file is generated by running `_TAXI_COMPLETE=source taxi > taxi-complete.bash`
  install -Dm644 taxi-complete.bash "$pkgdir"/usr/share/bash-completion/completions/taxi
  # Completion file is generated by running `_TAXI_COMPLETE=source_zsh taxi > taxi-complete.zsh`
  # and adding `#compdef taxi` on the first line
  install -Dm644 taxi-complete.zsh "$pkgdir"/usr/share/zsh/site-functions/_taxi
}
