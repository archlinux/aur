# Maintainer: gee

pkgname=ialauncher-git
pkgver=r127.b93f75d
pkgrel=1
pkgdesc='Play all of the Internet Archiveâ€™s MS-DOS games offline!'
arch=('x86_64')
url='https://github.com/rtts/ialauncher'
license=('GPL3')
depends=('dosbox' 'webkit2gtk' 'python' 'python-natsort' 'gst-plugins-bad'
         'python-jinja')
makedepends=('git' 'python-setuptools')
source=('ialauncher::git+https://github.com/rtts/ialauncher.git')
sha256sums=('SKIP')

pkgver() {
  cd ialauncher

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ialauncher

  python setup.py build
}

package() {
  cd ialauncher

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
