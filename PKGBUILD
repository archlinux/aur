# Generated by debtap
# Maintainer: Allen Zhong <allen@moe.cat>
# Contributor: Feishu Linux Team <contact@feishu.cn>
pkgname=feishu-bin
pkgver=7.11.8
#_pkgrel=24
_pkgtyp=stable
_pkghash_x64=73244d01
_pkghash_arm64=ba3b096c
pkgrel=1
pkgdesc="Linux client of Feishu (Lark) from Bytedance."
arch=('x86_64' 'aarch64')
url="https://www.feishu.cn/"
license=('unknown')
depends=('ca-certificates' 'gtk3' 'nss' 'xdg-utils' 'dnsmasq')
replaces=('bytedance-feishu-stable-bin')
conflicts=('feishu')
provides=('bytedance-feishu' 'feishu')
options=('!strip' '!emptydirs')
install=${pkgname}.install
source_x86_64=("https://sf3-cn.feishucdn.com/obj/ee-appcenter/${_pkghash_x64}/Feishu-linux_x64-${pkgver}.deb")
source_aarch64=("https://sf3-cn.feishucdn.com/obj/ee-appcenter/${_pkghash_arm64}/Feishu-linux_arm64-${pkgver}.deb")
sha256sums_x86_64=('75d9cda40c98e26718a49a7c1155af46b99c4a52b6a754309ffb40381ff354a0')
sha256sums_aarch64=('2db63408fb639b88270bc1815d0e529f7ddbbf1febf20025240b19eb9ff2c7ad')

package(){
  # Extract package data
  tar xpvf "${srcdir}/data.tar.xz" --xattrs-include='*' --numeric-owner -C "${pkgdir}"

  # Modify files
  cd "${pkgdir}"
  ln -s bytedance-feishu-${_pkgtyp} usr/bin/feishu

  sed -i "s/bytedance-feishu-${_pkgtyp}/feishu/g" "${pkgdir}/usr/share/applications/bytedance-feishu.desktop"
  sed -i 's/StartupNotify=true/StartupNotify=true\nStartupWMClass=feishu/g' "${pkgdir}/usr/share/applications/bytedance-feishu.desktop"

  sed -i "s/bytedance-feishu-${_pkgtyp}/feishu/g" "${pkgdir}/usr/share/menu/bytedance-feishu.menu"
  sed -i "s/bytedance-feishu/feishu/g" "${pkgdir}/usr/share/menu/bytedance-feishu.menu"

  sed -i 's/bytedance-feishu/feishu/g' "${pkgdir}/usr/share/appdata/bytedance-feishu.appdata.xml"

  sed -i 's/bytedance-feishu/feishu/g' "${pkgdir}/opt/bytedance/feishu/bytedance-feishu"

  mv "${pkgdir}"/usr/share/menu/{bytedance-,}feishu.menu
  mv "${pkgdir}"/usr/share/applications/{bytedance-,}feishu.desktop
  mv "${pkgdir}"/usr/share/appdata/{bytedance-,}feishu.appdata.xml
  mv "${pkgdir}"/usr/share/man/man1/{bytedance-feishu-${_pkgtyp},feishu}.1.gz
  mv "${pkgdir}"/usr/share/doc/{bytedance-feishu-${_pkgtyp},feishu}

  # Fix directory permissions
  find "${pkgdir}" -type d | xargs chmod 755
}
