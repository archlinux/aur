# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=biodbNcbi
_pkgver=1.6.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="a library for connecting to NCBI Databases."
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=(AGPL3)
depends=(
  r-biodb
  r-chk
  r-r6
  r-xml
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-biocstyle
  r-covr
  r-devtools
  r-knitr
  r-lgr
  r-rmarkdown
  r-roxygen2
  r-testthat
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('5d2c3e8eade431a86e13d54be468832e')
b2sums=('180507034121b1d2508967f24b37215d8e6031c53b786a55d9c8c23ca26fe061a7bd8d3ee6b8b679cae3a28f0e2f9ebbb0066300c26395e001bce2c4d642dfdc')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
