# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
pkgname=kokkos
pkgver=4.2.00
pkgrel=2
pkgdesc="C++ performance portability programming ecosystem"
arch=(x86_64)
url="https://github.com/${pkgname}/${pkgname}"
license=(Apache)
depends=(bash hwloc openmp)
makedepends=(cmake)
# checkdepends=(gtest)
conflicts=('trilinos')
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz)
sha512sums=('01a27163c34ce09b87ac612e94c09909174bab674051041a0056ddc4587a832b218a0dbb093becb85d28cf6b21ba1987ef86fd8b96692df16bed07934b46d1af')

build() {
  cmake \
    -S ${pkgname}-${pkgver} \
    -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=TRUE \
    -DCMAKE_CXX_STANDARD=17 \
    -DCMAKE_CXX_COMPILER=g++ \
    -DKokkos_ENABLE_HWLOC=ON \
    -DDKokkos_ENABLE_OPENMPTARGET=ON \
    -DKokkos_ENABLE_OPENMP=ON \
    -DKOKKOS_ENABLE_THREADS=ON \
    -DKOKKOS_ENABLE_SERIAL=ON \
    -DKokkos_ENABLE_TESTS=ON \
    -DKokkos_ENABLE_EXAMPLES=ON \
    -Wno-dev
  cmake --build build --target all
}

# check() {
#   ctest --test-dir build
# }

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
