# Maintainer: temidaradev <temidaradev@proton.me>
pkgname=kopuz
pkgver=0.6.5
pkgrel=1
pkgdesc="A modern music player"
arch=('x86_64')
url="https://github.com/temidaradev/kopuz"
license=('MIT')
depends=(
    'webkit2gtk-4.1'
    'gtk3'
    'libsoup3'
    'glib-networking'
    'alsa-lib'
    'openssl'
    'xdotool'
    'dbus'
    'cmake'
    'opus'
)
makedepends=(
    'rust'
    'cargo'
    'git'
    'dioxus-cli'
)
options=(!lto !debug)
# dioxus-cli must be installed manually or from AUR at version matching dioxus 0.7.x:
#   cargo install dioxus-cli --version "^0.7"
source=("$pkgname-$pkgver.tar.gz::https://github.com/temidaradev/kopuz/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('7c2fe94e34a7e7d0ca8a7e5d765c389f324bd37e8b6ccecd8f79ab30405db5eb')

build() {
    cd "$pkgname-$pkgver"

    export CARGO_HOME="$srcdir/cargo-home"
    export RUSTUP_TOOLCHAIN=stable

    # tailwind.css is pre-built and included in the release tarball
    dx build --release --platform desktop -p kopuz
}

package() {
    cd "$pkgname-$pkgver"

    local appdir="target/dx/kopuz/release/linux/app"

    install -Dm755 "$appdir/kopuz" "$pkgdir/usr/bin/kopuz"

    # Install bundled assets alongside binary
    if [[ -d "$appdir/assets" ]]; then
        cp -r "$appdir/assets" "$pkgdir/usr/bin/"
    fi

    install -Dm644 "data/com.temidaradev.kopuz.desktop" \
        "$pkgdir/usr/share/applications/com.temidaradev.kopuz.desktop"
    sed -i "s|Exec=kopuz|Exec=/usr/bin/kopuz|" \
        "$pkgdir/usr/share/applications/com.temidaradev.kopuz.desktop"

    install -Dm644 "crates/kopuz/assets/logo.png" \
        "$pkgdir/usr/share/icons/hicolor/256x256/apps/com.temidaradev.kopuz.png"

    install -Dm644 "data/com.temidaradev.kopuz.metainfo.xml" \
        "$pkgdir/usr/share/metainfo/com.temidaradev.kopuz.metainfo.xml"

    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
