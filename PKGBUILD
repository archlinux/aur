# Maintainer: Carlos Aznarán <caznaranl@uni.pe>
_base=setuptools-scm
pkgname=pypy3-${_base}
pkgdesc="the blessed package to manage your versions by scm tags"
pkgver=10.3.4
pkgrel=1
arch=(any)
url="https://github.com/pypa/${_base}"
license=(MIT)
depends=(pypy3-packaging pypy3-setuptools pypy3-vcs-versioning)
makedepends=(pypy3-build pypy3-installer)
source=(${url}/archive/${_base}-v${pkgver}.tar.gz)
sha512sums=('9ebe522510be5c68e62cc43ef8ff47ad10c8bcc6b0627d7b71b9bccd3839ce0c338a9771739e6cc48e6b3e1ed55bc79cc7836dd406ce0587c5b4e0da5c327a3d')

build() {
  cd ${_base}-${_base}-v${pkgver}
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  pypy3 -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${_base}-${_base}-v${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" pypy3 -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
