# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ksysguard
pkgver=5.19.0
pkgrel=2
pkgdesc='Track and control the processes running in your system'
arch=(x86_64)
url='https://userbase.kde.org/KSysGuard'
license=(LGPL)
depends=(libksysguard kinit)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('nvidia-utils: NVIDIA GPU usage')
groups=(plasma)
install=$pkgname.install
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
         ksysguard-dbus-name.patch::"https://invent.kde.org/plasma/ksysguard/-/commit/5391f4de.patch")
sha256sums=('abd56dcf96a3af1a23083044edc6dd6afb4726ab7bc3a64a1db1055554566f49'
            'SKIP'
            '9f5a9e6c0b2197cbaa7af82e3797ed7f1a6972e655826ffdb8de2d0b1e8a1175')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../ksysguard-dbus-name.patch # Fix dbus interface name
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
