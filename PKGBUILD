# Maintainer: Max Harmathy <harmathy@mailbox.org>
# Contributor: David Runge <dvzrv@archlinux.org>

_name=MediathekView
_pkgname=mediathekview
pkgname=${_pkgname}-xdg
pkgver=13.9.1
pkgrel=1
pkgdesc="Access the Mediathek of many German TV stations (store data in XDG_DATA_HOME)"
arch=(any)
url="https://github.com/mediathekview/mediathekview"
license=(GPL3)
depends=(bash hicolor-icon-theme java-runtime)
makedepends=(maven strip-nondeterminism)
conflicts=(mediathek mediathekview)
provides=(mediathek mediathekview)
replaces=(mediathek mediathekview)
optdepends=(
  'libnotify: to use desktop notifications'
  'mplayer: for recording streams'
  'vlc: for stream playback'
)
source=(
  $_pkgname-$pkgver.tar.gz::https://github.com/$_pkgname/$_pkgname/archive/refs/tags/$pkgver.tar.gz
  de.$_pkgname.$_name.desktop
  $_pkgname.sh
  flatlaf.version.patch
)
sha512sums=('f706a2fdf458b6dccec34b35df6b5fa831eba851ca694150f48f396b87a957315d3380648e59e2e9c76475010d6fdcda72cb8e7e42cceee2bc3b2524235bb7e6'
            '24a94a078180aca7c50ed7763ef4806c116c27f901f644ef570ee413ffc3ac795b5ebd24d696a9b2ec426e7c9b6eaf8a8b22addb5ac7c9fe9700f7c04305f64b'
            '6b840329a6906ebea3fca17f54511e89ac983526aee793e8c424bebe24234eead6933d62fc5cf27644c4aebeee22364c99d87adda2114d7fe492cf038003c26f'
            'd226773684ab40fd80a0088207c61e1c8475a6c121942f47d9734cacd7ab9c143af0ffe1b10529acd5dbbe25e1a7bc2b81b4b8316759fadf847a8f89240c93e3')
b2sums=('4b9826d1fbeceded9248e4938ae49c964637143b4bdb635f1f1b91388312a9506732ace34e92677edeaf635fd6b937a92259a692136e8ddf822e8b05c57abe10'
        '6dbcdea2918009621fc132b4ff1056ef79f06e27c3299b69ccd7e3cb2b093e3a2a5f76acd6b1ab62689edd867ac1650f61bf829f2a1c575835d31e117d9b9ae5'
        '6d33097cd2007264de8d2a0e0ebdb2ea3ac911b12e94befb780add3083a30b84437315394e491e5e63f66c4c93eaa5d8f9e50e70be42d109cac17caf53fe86c3'
        '906ee41b1f83c608458f885897f408bfb569b6c608f9f55be8c8aaa196b958c86cf5ae8d219d5216eb9db865b0abb17466ba0d6fcab659767f64ec78e48db2e6')

build() {
  cd $_name-$pkgver
  patch -p1 < "$startdir/flatlaf.version.patch"
  ./mvnw clean install -Plinux,install4j-linux
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  local _size

  cd $_name-$pkgver
  # jar
  install -vDm 644 target/$_name.jar -t "$pkgdir/usr/share/java/$_pkgname/"
  # script
  install -vDm 755 ../$_pkgname.sh "$pkgdir/usr/bin/$_pkgname"
  # XDG desktop file
  install -vDm 644 ../de.mediathekview.$_name.desktop -t "$pkgdir/usr/share/applications/"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 target/${_name}@x$_size.png "$pkgdir/usr/share/icons/hicolor/${_size}x$_size/apps/$_pkgname.png"
  done
  install -vDm 644 res/$_name.svg -t "$pkgdir/usr/share/icons/hicolor/scalable/apps/$_pkgname.svg"
  # docs
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$_pkgname/"
}
