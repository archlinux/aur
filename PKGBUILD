# Maintainer: liixini <https://github.com/liixini>
pkgname=skwd-wall
pkgver=r30.a8d3082
pkgrel=1
pkgdesc='Quickshell-based image, video & wallpaper engine wallpaper selector with color sorting, Matugen integration, and Wallhaven & Steam in-app browsing'
arch=('any')
url='https://github.com/liixini/skwd-wall'
license=('MIT')
depends=(
  'skwd-daemon'
  'quickshell'
  'qt6-multimedia'
  'qt6-declarative'
  'qt6-imageformats'
  'awww'
  'mpvpaper'
  'matugen'
  'ttf-nerd-fonts-symbols'
  'ttf-roboto'
)

install="${pkgname}.install"
source=("${pkgname}::git+https://github.com/liixini/skwd-wall.git#branch=experimental/rust-refactor")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$pkgname"

  local _sharedir="$pkgdir/usr/share/skwd-wall"

  install -dm755 "$_sharedir"
  cp -a shell.qml qml/ "$_sharedir/"

  install -dm755 "$_sharedir/data"
  cp -a data/matugen/ "$_sharedir/data/"
  cp -a data/scripts/ "$_sharedir/data/"
  install -Dm644 data/config.json.example "$_sharedir/data/config.json.example"

  install -Dm644 data/skwd-wall.desktop "$pkgdir/usr/share/applications/skwd-wall.desktop"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
