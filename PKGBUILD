# Maintainer: tocic <tocic at protonmail dot ch>
# Contributor: Bart Louwers <sleeping@emeel.net>

pkgname=ut
pkgver=1.1.9
pkgrel=1
pkgdesc="UT: C++20 Î¼(micro)/Unit Testing Framework"
arch=("any")
url="https://boost-ext.github.io/ut"
license=("Boost")
makedepends=("cmake")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/boost-ext/ut/archive/v${pkgver}.tar.gz")
b2sums=("9439d4f9466c76f6bf225d202a531dfd43585d2d13e53ae4a728a7403c62efccf7a1fb772ba4279823e22ec0bba9442df6330f0963d6cc57cb4328c8e2fbe9c3")

build() {
  cmake -B "build/" -S "${pkgname}-${pkgver}" \
    -D BOOST_UT_BUILD_BENCHMARKS:BOOL="OFF" \
    -D BOOST_UT_BUILD_EXAMPLES:BOOL="OFF" \
    -D BOOST_UT_BUILD_TESTS:BOOL="OFF" \
    -D CMAKE_INSTALL_PREFIX:PATH="/usr/" \
    -Wno-dev

  cmake --build "build/"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build/"
}
