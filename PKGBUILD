# Maintainer: Roshless <pkg@roshless.com>

pkgname=lagrange
pkgrel=2
pkgver=1.20.0
pkgdesc="Beautiful Gemini Client"
url="https://git.skyjake.fi/skyjake/lagrange"
arch=('x86_64' 'i686' 'aarch64' 'armv7h')
license=("BSD")
install="$pkgname.install"
source=(
    "https://git.skyjake.fi/skyjake/$pkgname/releases/download/v$pkgver/lagrange-$pkgver.tar.gz"
    fix-build.patch
)

depends=(
    "fribidi"
    "harfbuzz"
    "hicolor-icon-theme"
    "libunistring"
    "openssl"
    "pcre"
    "sdl2"
    "zlib"
    "mpg123"
)
makedepends=(
    "cmake"
    "zip"
)
optdepends=(
    "libwebp: Webp decode support"
    "libjxl: JPEG XL image decoding"
)

prepare() {
    patch -d "$pkgname-$pkgver/lib/the_Foundation" -Np1 -i ../../../fix-build.patch
}

build() {
    cmake -B build -S "$pkgname-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DENABLE_KERNING=OFF \
        -DTFDN_ENABLE_WARN_ERROR=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -Wno-dev
    make -C build
}

package() {
    install -Dm644 $pkgname-$pkgver/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    cd build
    make DESTDIR="$pkgdir" install
}

sha256sums=('fd19217f0f2706addf6e6a82260e9f62bab799ef35c43faaa305232efe4ebab9'
            '7afe6818c841d44899b41873d7279b96e4dbebc6825495229c51de25aef1894f')
