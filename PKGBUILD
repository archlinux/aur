# Maintainer: Maksim Kurnosenko <asusx2@mail.ru>

pkgname=anylogic-professional
pkgver=8.9.4
pkgrel=1
pkgdesc="AnyLogic Professional Edition - for companies and government organizations"
arch=(x86_64)
url="https://www.anylogic.com"
license=('custom')
depends=('glibc')
optdepends=('chromium: alternative browser for model animating'
            'firefox: alternative browser for model animating'
            'webkitgtk: it may be required because of libwebkitgtk-1.0 dependency (not building as of now, only binary)')
provides=('anylogic')
conflicts=('anylogic')
options=(!strip)
source=("https://files.anylogic.com/$pkgname-$pkgver.linux.x86_64.tgz.bin"
        "$pkgname.desktop")
sha256sums=('11dfab1e24bdf9a84a40ee09b4c2b1ce3c74d456ad4c397cecaf45087f35044c'
            '3d1c428d84b20cfc7ddd8977cf00e732893881de2295d326b72cd57b0568102b')

prepare() {
    msg2 "Unpacking archive..."
    tail -n +511 "$srcdir/$pkgname-$pkgver.linux.x86_64.tgz.bin" > "$pkgname-$pkgver.linux.x86_64.tgz"

    msg2 "Extracting archive..."
    tar -xf $pkgname-$pkgver.linux.x86_64.tgz
}

package() {
    mkdir -p "$pkgdir/opt"

    msg2 "Copying AnyLogic Professional Edition contents..."
    cp -R "$srcdir/anylogic" "$pkgdir/opt"

    install -Dm644 "$srcdir/anylogic/icon.xpm" "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
    install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

    install -Dm644 "$srcdir/anylogic/license/Software Licensing Agreement for AnyLogic.txt" "$pkgdir/usr/share/licenses/anylogic/LICENSE"
}
