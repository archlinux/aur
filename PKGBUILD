# Maintainer: Atte Lautanala <atte@lautana.la>
# Contributor bageljr <bageljr 897 at protonmail dot com>
# PKGBUILD generated by pipman
# Python package author: Lie Ryan <lie.1296@gmail.com>
pkgname=python-pylsp-rope
_name=${pkgname#python-}
pkgver=0.1.15
pkgrel=1
pkgdesc="Extended refactoring capabilities for Python LSP Server using Rope."
arch=(any)
url="https://github.com/python-rope/pylsp-rope"
license=('MIT')
depends=("python-rope" "python-typing_extensions" "python" "python-lsp-server")
makedepends=("python-pip" "python-build" "python-installer" "python-pytest" "python-wheel")
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('d1fd16cc971539f1f569b267bd908b339fd6d497d5c520c01fe67500303a9144')
build() {
	cd $_name-$pkgver
	python -m build --wheel --no-isolation
}

package() {
	cd $_name-$pkgver
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
check() {
	cd $_name-$pkgver
	python -m pytest .
}
