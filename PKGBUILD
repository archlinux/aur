# Maintainer: SpacingBat3 <git@spacingbat3.anonaddy.com>
_suffix="-arkenfox-autoconfig"
pkgbase=gecko$_suffix
pkgname=() # Will be generated by shell
pkgver=112.0
pkgrel=2
pkgdesc="A split package which installs arkenfox as autoconfig.js policy on many browsers."
arch=(any)
url="https://github.com/arkenfox/user.js"
license=('MIT')

_gist_author=SpacingBat3
_gist_id=d9b56e0ee88f7a7201e6a45f4858b5e0
_gist_rev=d37c1853333fb7448ecb7124671a40f792751cac

_gh_author=arkenfox
_gh_name=user.js
_gh_rev=$pkgver

source=("autoconfig.js"
        "https://gist.githubusercontent.com/$_gist_author/$_gist_id/raw/$_gist_rev/ujs2cfg.sh"
        "https://raw.githubusercontent.com/$_gh_author/$_gh_name/$_gh_rev/user.js"
        "https://raw.githubusercontent.com/$_gh_author/$_gh_name/$_gh_rev/LICENSE.txt")
md5sums=('f500bce7ef057a7db6c78bed2fe38609'
         '121a7c8c67b8668f8553fcc49e18b4b2'
         '4b076afb0bab5dd9c5e5566f3adee897'
         '05ef200686826459427e9769f7aa2c53')
sha512sums=('695695e1279b829b4999bd0c55ad9ab5a1fca4bf67a58568c072a324781df0a712597192005ed69dc63d22cd58bd057d24858a3e445518265d785b0d0f24a9ed'
            '7d66fce8be8ac9ec2df5fe14c5492401e6d5deb04134232e94358f6bd2535ec14245d22194d9da6c93b972fa12d66e0029be8d233c1e0e497d673fd59a1674ec'
            '4ed527c990e9f74b994e47da1c71191184f3492b2b8bb178e3ba6a186dce4366cbda25a4e5edc4fee1ee4450aef3877a6d990fe708166491c3dc3ff37d86ff50'
            'e6bda049f5a259d23026c2e7f294d6c0e7eec3be21e07e734e83c89f1509df62112b4d4ef6474593e564a55ed511d17f9294a22b47ebe4dade825c92100b4e1d')
b2sums=('6b379e58ee3768d70cd6abdd61e36c4c01f2903ec2c24bcdae4a71d5e5f1c1961eeced5e5c63474566c9369e50f28338066e1ad3ecdf5bcbd54630b9d510340b'
        'dc413423d7c55223b8dbb61f8417ea199ed53af910d3bdd29add4c0dd2bf9a011535125fbbd74c1a3031be48d48e91f9d63ef4be21e214c8c428014d64595e7c'
        '589ae7603e3221ce0f36a0e7b213b6343a86d2cb0bc66f82277be59cb5c9e78953cfdefdebc031a9c940d4447cdebefc9e8b15c2f37ce8b4125cbd5c90ac4d77'
        'c6a031cb7012627068355909c9ffd70a8d94edc9ddb5834fc6d7722856b21747671f5070c59e73b240303346ee98d117e8cfc656d11caddebf61ac43516f4cc2')

# List of browsers, which install their data in /usr/lib/
_browsers_lib=({firefox{,-esr,-developer-edition},icecat,palemoon})

# List of browsers, which install their data in /opt/
_browsers_opt=(waterfox-g{,4,3})

# Common package description.
_cdesc() {
	echo "Use arkenfox user.js system-wide, as $1 autoconfig.js policy"
}

build() {
	printf '%s' "- Generating a '.cfg' policy from 'user.js' file..."
	sh "$srcdir/ujs2cfg.sh" "$srcdir/user.js" > "$srcdir/arkenfox.cfg"
	echo " DONE!"
}

_package() {
	install -dm755 "$pkgdir/$2/$1/defaults/pref/"
	install -Dm644 "$srcdir/autoconfig.js" "$pkgdir/$2/$1/defaults/pref/autoconfig.js"
	install -Dm644 "$srcdir/arkenfox.cfg" "$pkgdir/$2/$1/arkenfox.cfg"
	install -Dm644 "$srcdir/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

_count=0
# Generate `pkgname` and `package_XXX()`:
for _browser in "${_browsers_lib[@]}" "${_browsers_opt[@]}"; do
	pkgname+=("$_browser$_suffix")
	[ $_count -lt "${#_browsers_lib[@]}" ] && _path='/usr/lib' || _path='/opt'
	_count=$(($_count+1));
	if [ "$DEBUG" = true ]; then
		echo "- Generating scripts for $_browser, installed in $_path..." >&2
	fi
	# Yes, we eval because we're too lazy to type such stuff and update it by hand.
	#
	# It should be safe tho, as most variables store predictible values and
	# we generate stuff at top-level of script, so makepkg should not be able
	# to mess with stuff at that point nor even have a way to stop it if.
	#
	eval \
"package_$_browser$_suffix(){
	pkgdesc='$(_cdesc "$_browser")'
	provides=('$_browser'-{autoconfig,policy});
	conflicts=('$_browser'-{autoconfig,policy} '$_browser'-user-autoconfig);
	depends=('$_browser');
	_package '$_browser' '$_path';
}"
done