# Maintainer: zxp19821005 <zxp19821005 at 163 dot com>
pkgname=deskgpt-bin
pkgver=1.0.0
pkgrel=2
pkgdesc="A desktop client for OpenAI's ChatGPT, providing a seamless experience across Linux, Windows, and MacOS."
arch=("x86_64")
url="https://github.com/0x11c11e/DeskGPT"
license=("GPL3")
provides=("${pkgname%-bin}=${pkgver}")
conflicts=("${pkgname%-bin}")
depends=(
    'electron26'
)
source=(
    "${pkgname%-bin}-${pkgver}.deb::${url}/releases/download/v${pkgver}/${pkgname%-bin}_${pkgver}_amd64.deb"
    "${pkgname%-bin}.sh"
)
sha256sums=('aa3ba0bcec46df1d286893b2805898357a426c83c45fe8b3afec416c2386a30a'
            '37ac61ff6b7f26f9d9a7692b1f862ffb345cbc015b8d1c7e7cdffab0ddb1701a')
build() {
    bsdtar -xf "${srcdir}/data.tar.xz"
    sed "s|/opt/${pkgname%-bin}/${pkgname%-bin} %U|${pkgname%-bin}|g" -i "${srcdir}/usr/share/applications/${pkgname%-bin}.desktop"
}
package() {
    install -Dm755 "${srcdir}/${pkgname%-bin}.sh" "${pkgdir}/usr/bin/${pkgname%-bin}"
    install -Dm644 "${srcdir}/opt/${pkgname%-bin}/resources/app.asar" -t "${pkgdir}/usr/lib/${pkgname%-bin}"
    install -Dm644 "${srcdir}/usr/share/applications/${pkgname%-bin}.desktop" -t "${pkgdir}/usr/share/applications"
    install -Dm644 "${srcdir}/usr/share/icons/hicolor/0x0/apps/${pkgname%-bin}.png" -t "${pkgdir}/usr/share/pixmaps"
}