# Maintainer: Nym <ops@nymte.ch>
# Maintainer: Andrej Mihajlov <andrej@nymtech.net>
# Maintainer: Lawrence Stalder <lawrence.stalder@pm.me>
# Contributor: Pierre Dommerc <pierre@nymtech.net>

pkgname=nym-vpnd
pkgver=2026.12.2
pkgrel=1
pkgdesc='NymVPN daemon as a systemd service'
arch=('x86_64' 'aarch64')
url='https://github.com/nymtech/nym-vpn-client'
license=('GPL-3.0-only')
depends=('glibc' 'gcc-libs' 'dbus' 'libmnl' 'libnftnl' 'polkit')
makedepends=('rust' 'cargo' 'go' 'protobuf')
provides=('nym-vpnd' 'nym-exclude' 'nym-socks5-proxy')
conflicts=('nym-vpnd')
options=(!debug)
source=("$url/archive/refs/tags/nym-vpn-v2026.12.2.tar.gz" 'nym-vpnd.service' 'nym-vpn.conf')
sha256sums=(c413d41b95a72e050761f07e103391cbeec82406d61cfec28691ca73b491dc34 66d5b043cbef2ae0ba19cc7685c7b42808515b8b520b0dd15a0c313ca039f6d6 af03cbdb98708e60038d784503d99f382595f3a1e020fea41d5c2cc5800de319)
_srcdir="nym-vpn-client-nym-vpn-v2026.12.2"

prepare() {
  pushd "$_srcdir"
  # remove unused sources
  rm -rf nym-vpn-android
  rm -rf nym-vpn-apple
  rm -rf nym-vpn-app
  rm -rf nym-vpn-windows
  popd
}

build() {
  pushd "$_srcdir"

  # build wireguard-go
  pushd wireguard
  ./build-wireguard-go.sh
  popd

  pushd nym-vpn-core
  # 1. sqlx does not support LTO build flag, which is enabled by default in Arch
  # set the C flag -ffat-lto-objects to solve the issue
  # see https://github.com/launchbadge/sqlx/issues/3149
  #
  # 2. aws-lc-sys v0.38: build issues related to optimizations (fixed in newer versions)
  # see: https://github.com/aws/aws-lc-rs/pull/1064
  CFLAGS+=" -ffat-lto-objects -O0" cargo build --release --locked
  popd

  popd # _srcdir
}

package() {
  pushd "$_srcdir/nym-vpn-core/target/release"
  install -Dm755 "nym-vpnd" "$pkgdir/usr/bin/nym-vpnd"
  install -Dm755 "nym-exclude" "$pkgdir/usr/bin/nym-exclude"
  chmod u+s "$pkgdir/usr/bin/nym-exclude"
  install -Dm755 "nym-socks5-proxy" "$pkgdir/usr/bin/nym-socks5-proxy"
  popd

  install -Dm644 nym-vpnd.service "$pkgdir/usr/lib/systemd/system/nym-vpnd.service"
  install -Dm644 nym-vpn.conf "$pkgdir/usr/lib/sysusers.d/nym-vpn.conf"
}
