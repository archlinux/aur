# Maintainer: Zubarev Grigoriy <thirtysix@thirtysix.pw>

pkgname=walogram-git
_pkgname="${pkgname%-git}"
pkgver=1.3.0.r70.d985fdd
pkgrel=1
pkgdesc='Generate telegram-desktop themes based on generated by wal or user defined colors'
url="https://codeberg.org/thirtysixpw/$_pkgname"
arch=('any')
license=('MIT')
makedepends=('git')
depends=('imagemagick' 'zip')
optdepends=('python-pywal: generate color scheme from image')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+$url")
sha256sums=('SKIP')

pkgver() {
	cd "$_pkgname"
	printf '%s.r%s.%s\n' "$(grep -Po '^\s*VERSION\s*=\s*\K(.*)$' Makefile)" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$_pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: ts=4 sw=4 noet
