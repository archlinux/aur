# Generated by debtap
# Maintainer: Peter B <aur at boromisp dot com>
# Contributor: Caio Prado <cagprado at qq dot com>
#
# Initial setup thanks to debtap (https://github.com/helixarch/debtap)
pkgname=vyprvpn-linux-cli
pkgver=1.8
pkgrel=1
pkgdesc="A simple service and command line interface to manage NetworkManager based connections to VyprVPN."
arch=('i686' 'x86_64')
url="http://www.goldenfrog.com"
license=('unknown')
depends=('networkmanager' 'networkmanager-openvpn' 'networkmanager-pptp' 'networkmanager-vpnc')
options=('!strip' '!emptydirs')

backup=('etc/vyprvpn/vyprvpn_service.conf')
install=$pkgname.install

source=("$pkgname.install")
source_i686=("https://support.goldenfrog.com/hc/article_attachments/360054309192/vyprvpn-linux-cli-1.8.i386.deb")
source_x86_64=("https://support.goldenfrog.com/hc/article_attachments/360054449151/vyprvpn-linux-cli-1.8.amd64.deb")

sha256sums=('310cfdcf3d8b84789923139f3ce918003ad743bddf8bed72bc23bef90a43240b')
sha256sums_i686=('a5b6a19faa3cb49b8850c79e5f99daf3e995250c286dd575e2aa37b06b9e8ae5')
sha256sums_x86_64=('c2c67794b6f4bcdf5f2e3d1e8eeab6a2bff9f452ccc70032b9a408c9f98d5511')

prepare(){
	# Extract data
	tar xf data.tar.xz
}

package(){
	install -d "$pkgdir"/usr
	cp -R "$srcdir"/usr/bin "$pkgdir"/usr
	cp -R "$srcdir"/usr/share "$pkgdir"/usr
	cp "$srcdir"/usr/sbin/* "$pkgdir"/usr/bin
	chmod 755 "$pkgdir"/usr/bin/*

	install -d "$pkgdir"/etc
	install -d "$pkgdir"/usr/lib
	cp -R "$srcdir"/etc/vyprvpn "$pkgdir"/etc
	cp -R "$srcdir"/etc/systemd "$pkgdir"/usr/lib
	chmod 644 "$pkgdir"/usr/lib/systemd/system/vyprvpn.service

	touch "$pkgdir"/etc/vyprvpn/vyprvpn_service.conf
	chmod 600 "$pkgdir"/etc/vyprvpn/vyprvpn_service.conf

	sed -i '/ExecStop/c\KillMode=process' "$pkgdir"/usr/lib/systemd/system/vyprvpn.service
}
