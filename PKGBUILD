# Maintainer: Taboon Egon <te451 -_AT_- netcourrier -_DOT_- com>
# Contributor: Narrat <address??? at domain??? dot tld???>
# Contributor: der_fenix <derfenix -_AT_- gmail -_DOT_- com>
pkgname=semantik
pkgver=1.2.11
pkgrel=1
pkgdesc="mindmapping-like tool for document generation (reports, thesis, etc)"
arch=('x86_64')
url="https://waf.io/semantik.html"
license=('GPL3')
depends=('python' 'qt5-webengine' 'qt5-svg' 'qt5-tools' 'kdelibs4support' 'kio5' 'python-pygments')
makedepends=('shared-mime-info' 'desktop-file-utils')
optdepends=('minted: syntax highlighting support in documents generated by LaTeX')
source=("https://waf.io/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('3d097f0f210433b74ca97288612d04032d482a61f299072ca830ab68037e49b6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./waf configure clean build
  gzip src/data/semantik.1
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./waf install --destdir=${pkgdir} --nomimes --noldconfig --nogtkicons
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/example.sem" "$pkgdir/usr/share/doc/$pkgname/example.sem"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/src/data/$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
}
