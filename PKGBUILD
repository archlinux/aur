# Maintainer: Pekka Ristola <pekkarr [at] protonmail [dot] com>

_pkgname=biodbExpasy
_pkgver=1.6.0
pkgname=r-${_pkgname,,}
pkgver=${_pkgver//-/.}
pkgrel=3
pkgdesc="a library for connecting to Expasy ENZYME database."
arch=(any)
url="https://bioconductor.org/packages/$_pkgname"
license=(AGPL3)
depends=(
  r-biodb
  r-chk
  r-r6
  r-stringr
)
checkdepends=(
  r-testthat
)
optdepends=(
  r-biocstyle
  r-covr
  r-devtools
  r-knitr
  r-lgr
  r-rmarkdown
  r-roxygen2
  r-testthat
)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_pkgname}_${_pkgver}.tar.gz")
md5sums=('b4c2596f2efc76e3ba7cba00fb3a6177')
b2sums=('c289e80fcffd0de564c896434c0f75a8d2ed4ae1153101dee3cf89abba6a640fc2e660da307769a2f7cf9a6190858ff2019acda6b9a08d13c2e944ceb796bfe3')

build() {
  mkdir build
  R CMD INSTALL -l build "$_pkgname"
}

check() {
  cd "$_pkgname/tests"
  R_LIBS="$srcdir/build" NOT_CRAN=true Rscript --vanilla testthat.R
}

package() {
  install -d "$pkgdir/usr/lib/R/library"
  cp -a --no-preserve=ownership "build/$_pkgname" "$pkgdir/usr/lib/R/library"
}
