# Maintainer: TÃ©rcio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>

pkgname=r3d-sdk
pkgver=8.5.1
pkgrel=1
arch=('x86_64')
pkgdesc="SDK to load and decode R3D files generated by RED cameras"
url="https://www.red.com/developers"
license=('custom')
depends=('gcc-libs' 'util-linux-libs')
makedepends=('poppler')
options=('!strip')
source=("https://downloads.red.com/software/sdk/R3D/${pkgver%.*}/R3DSDKv${pkgver//./_}.zip")
b2sums=('50d169df410974f7a5765feac48b0d313aeac04139243972b719987b892c93e736e208418bf53ac9b1b8be7a18195b65ce92297b2c41374022c948af63aaccff')

prepare(){
  pdftotext -layout "R3DSDKv${pkgver//./_}/SDK License Agreement.pdf" 2>&-
  mv "R3DSDKv${pkgver//./_}/SDK License Agreement.txt" "$srcdir"
}

package() {
  install -d "$pkgdir/opt"
  install -d "$pkgdir/usr/share/licenses/$pkgname"

  cp -dr --no-preserve=ownership "R3DSDKv${pkgver//./_}" \
                                 "$pkgdir/opt/R3DSDK"

  cp --no-preserve=ownership "$srcdir/SDK License Agreement.txt" \
                             "$pkgdir/usr/share/licenses/$pkgname"
}
