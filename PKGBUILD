# Maintainer: willemw <willemw12@gmail.com>
# Contributor: Sian1468 <sian1468-aur@.39011468.xyz>
# Contributor: Julien Nicoulaud <julien DOT nicoulaud AT gmail DOT com>

pkgname=ffmpeg-bitrate-stats
pkgver=1.3.2
pkgrel=1
pkgdesc='Calculate bitrate statistics using FFmpeg'
arch=(any)
url=https://github.com/slhck/ffmpeg-bitrate-stats
license=(MIT)
depends=(ffmpeg python-colorlog python-mutagen python-tqdm python-plotille python-ffmpeg-progress-yield)
checkdepends=(git python-pytest uv)
makedepends=(git python-installer python-uv-build uv)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz")
sha512sums=('fedfffde969a8b6a8927810c1aa72be86af1e8efb5943ee6021415f762794c99b4324d25fb08cac3fe93ca30d5e46e98bc9b3534af3c492191ce63922502ba16')

#prepare() {
#  rm -rf $pkgname-$pkgver/dist
#}

build() {
  cd $pkgname-$pkgver
  #python -m build --no-isolation --wheel
  uv --no-cache --no-managed-python --offline build --no-build-isolation --out-dir=dist --wheel .
}

check() {
  cd $pkgname-$pkgver
  uv venv --no-managed-python --system-site-packages
  uv --no-cache --no-managed-python --offline pip install --link-mode=copy --no-deps dist/*.whl
  PATH=".venv/bin:$PATH" ./.venv/bin/python -m pytest tests
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
