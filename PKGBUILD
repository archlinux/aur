# Maintainer: Chan Beom Park <cbpark@gmail.com>

_hkgname=threadscope
pkgname=haskell-threadscope
pkgver=0.2.14
pkgrel=1
pkgdesc="A graphical viewer for thread profile information generated by the Glasgow Haskell compiler (GHC)."
url="http://hackage.haskell.org/package/${_hkgname}"
arch=('x86_64')
license=('custom:BSD3')
depends=('ghc-libs'
         'haskell-cairo'
         'haskell-ghc-events'
         'haskell-glib'
         'haskell-gtk2'
         'haskell-mtl'
         'haskell-pango'
         'haskell-text'
         'haskell-file-embed'
         'haskell-temporary')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('23e87e3454af1ac353132dd1929281303ff5575269059b2a4d74032c8eb26153d176751fabc18550338dba5bea0ab8a39f8644b55194670a16d262b6bf56d437')

prepare() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    sed -i 's/gtk >= 0.12 && < 0.16,/gtk,/' threadscope.cabal
    sed -i 's/ghc-events >= 0.13 && < 0.16,/ghc-events,/' threadscope.cabal
    sed -i 's/template-haskell < 2.16,/template-haskell,/' threadscope.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
