# Merged with official ABS frameworkintegration PKGBUILD by João, 2021/02/01 (all respective contributors apply herein)
# Maintainer: João Figueiredo & chaotic-aur <islandc0der@chaotic.cx>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=frameworkintegration-git
pkgver=6.0.0_r749.gf63d476
pkgrel=2
pkgdesc='Framework providing components to allow applications to integrate with a KDE Workspace'
arch=($CARCH)
url='https://community.kde.org/Frameworks'
license=(LGPL-2.0-only LGPL-3.0-only)
depends=(gcc-libs glibc kcolorscheme-git kconfig-git ki18n-git kiconthemes-git knewstuff-git knotifications-git kwidgetsaddons-git qt6-base)
makedepends=(git appstream-qt extra-cmake-modules-git packagekit-qt6)
optdepends=('appstream-qt: dependency resolving via AppStream'
            'packagekit-qt6: dependency resolving via AppStream')
conflicts=(${pkgname%-git})
provides=(${pkgname%-git})
groups=(kf6-git)
source=("git+https://github.com/KDE/${pkgname%-git}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(KF_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${pkgname%-git} \
    -DQT_MAJOR_VERSION=6 \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
