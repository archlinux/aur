# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgdesc='Omron LUNA-I/LUNA-88K, Sharp X68030, and NEWS NWS-1750 emulator'
pkgname=nono
pkgver=0.5.0
pkgrel=1
url=http://www.pastel-flower.jp/~isaki/nono
license=(custom)
arch=(x86_64)
depends=(libbsd libkqueue termcap wxgtk3)
makedepends=(bmake freebsd-mk gettext)
source=("${url}/archive/${pkgname}-${pkgver}.tar.gz")
b2sums=('612ae3e6789e66934b94adfc2d1e7c919f99accf7b373d49e17971eb52b5802c3eb9095885a47c2900a31bee9fdeaace27c4a37f172a1f119c336fde82efe695')

prepare ()
{
	# The configure script searches for <kqueue/sys/event.h> instead of the
	# regular location <sys/event.h> as used by the libkqueue package, so
	# provide a local directory with the expected layout.
	mkdir -p "${srcdir}/include/kqueue/"
	ln -vsnf /usr/include/sys "${srcdir}/include/kqueue/sys"
}

build ()
{
	cd "${pkgname}-${pkgver}"

	./configure \
		CPPFLAGS="${CPPFLAGS} -I${srcdir}/include" \
		WX_CONFIG=/usr/bin/wx-config-gtk3 \
		--prefix=/usr

	# The configuration generated by autoconf does NOT include CPPFLAGS,
	# so add the include directory for the mangled libkqueue headers now.
	sed -i "/^C\(C\|XX\)=/s,$, -I${srcdir}/include," \
		Makefile.cfg

	bmake -DNOTEST -m/usr/share/mk-freebsd
}

package ()
{
	cd "${pkgname}-${pkgver}"
	bmake -DNOTEST -m/usr/share/mk-freebsd DESTDIR="${pkgdir}" install
	install -Dm644 doc/nono-license.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
